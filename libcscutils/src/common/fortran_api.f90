MODULE CSC_COMMON
    USE ISO_C_BINDING
    IMPLICIT NONE


    INTERFACE
        FUNCTION CSC_WTIME_C() BIND(C,name="csc_wtime")
            IMPORT
            REAL(KIND=C_DOUBLE) ::  CSC_WTIME_C
        END FUNCTION CSC_WTIME_C

        FUNCTION CSC_CTIME_C() BIND(C,name="csc_ctime")
            IMPORT
            REAL(KIND=C_DOUBLE) ::  CSC_CTIME_C
        END FUNCTION CSC_CTIME_C

        FUNCTION CSC_CYCLES_C() BIND(C,name="csc_cycles")
            IMPORT
            INTEGER(KIND=C_INT64_T) :: CSC_CYCLES_C
        END FUNCTION CSC_CYCLES_C

        FUNCTION CSC_MTX_WRITE_DOUBLE_C(FILENAME, M, N, A, LDA) BIND(C, name="csc_mtx_write_double")
            IMPORT
            INTEGER(KIND=C_INT) :: CSC_MTX_WRITE_DOUBLE_C
            CHARACTER(KIND=C_CHAR), INTENT(IN) :: FILENAME(*)
            INTEGER(KIND=C_INT), INTENT(IN), VALUE :: M
            INTEGER(KIND=C_INT), INTENT(IN), VALUE :: N
            INTEGER(KIND=C_INT), INTENT(IN), VALUE :: LDA
            REAL(KIND=C_DOUBLE), INTENT(IN) :: A(LDA, *)
        END FUNCTION CSC_MTX_WRITE_DOUBLE_C

    END INTERFACE
CONTAINS

    DOUBLE PRECISION FUNCTION CSC_WTIME()
        IMPLICIT NONE
        DOUBLE PRECISION TMP

        TMP = CSC_WTIME_C()

        CSC_WTIME = DBLE(TMP)
        RETURN
    END FUNCTION CSC_WTIME

    DOUBLE PRECISION FUNCTION CSC_CTIME()
        IMPLICIT NONE
        DOUBLE PRECISION TMP

        TMP = CSC_CTIME_C()

        CSC_CTIME = DBLE(TMP)
        RETURN
    END FUNCTION CSC_CTIME

    INTEGER FUNCTION CSC_CYCLES()
        IMPLICIT NONE
        INTEGER(C_INT64_T) :: TMP

        TMP = CSC_CYCLES_C()
        CSC_CYCLES = INT(TMP)
        RETURN
    END FUNCTION CSC_CYCLES

    CHARACTER(LEN=20) FUNCTION INT2STR(K)
        INTEGER, INTENT(IN) :: K
        WRITE (INT2STR, *) K
        INT2STR = TRIM(ADJUSTL(INT2STR))
    END FUNCTION INT2STR

    CHARACTER(LEN=25) FUNCTION DBLE2STR(K)
        DOUBLE PRECISION, INTENT(IN) :: K
        WRITE (DBLE2STR,'( D15.7 )') K
        DBLE2STR = TRIM(ADJUSTL(DBLE2STR))
    END FUNCTION DBLE2STR

    SUBROUTINE CSC_MTX_WRITE_DOUBLE(FILENAME, M, N, A, LDA, INFO)
        IMPLICIT NONE
        CHARACTER(LEN=*), INTENT(IN) :: FILENAME
        INTEGER, INTENT(IN) :: M, N, LDA
        INTEGER, INTENT(OUT) :: INFO
        DOUBLE PRECISION, INTENT(IN) :: A(LDA, *)

        INTEGER(KIND=C_INT) :: RET

        RET = CSC_MTX_WRITE_DOUBLE_C(TRIM(FILENAME)//C_NULL_CHAR, INT(M,KIND=C_INT), INT(N, KIND=C_INT), &
            & A, INT(LDA, KIND=C_INT))

        IF ( RET .NE. 0 ) THEN
            INFO = 1
        ELSE
            INFO = 0
        END IF
        RETURN
    END SUBROUTINE CSC_MTX_WRITE_DOUBLE

END MODULE CSC_COMMON
